# Template for = operator.
# Args:
#   $a0: pointer to left instance
#   $a1: pointer to right instance
# Returns:
#   $v0: 1 if the instance are Int or Bool and have the same value,
#       or the instance are of other type and share the same pointer;
#       0 in other case.
equal:
    beq     $a0, $a1, are_equal
    lw      $t0, 0($a0)
    # check if the type is Int
    la      $t1, Int
    beq     $t0, $t1, by_value
    # check if the type is Bool
    la      $t1, Bool
    beq     $t0, $t1, by_value
    # check if the type is String
    la      $t1, String
    beq     $t0, $t1, compare_str
    j		are_not_equal
by_value:
    lw      $t0, 4($a0)
    lw      $t1, 4($a1)
    beq     $t0, $t1, are_equal
are_not_equal:
    li      $v0, 0
    j       end_equal
are_equal:
    li      $v0, 1
end_equal:
    jr      $ra
# instructions for string content comparison
compare_str:
    lw      $t0, 4($a0)
    lw      $t1, 4($a1)
compare_str_loop:
    lb      $t2, 0($t0)
    lb      $t3, 0($t1)
    bne     $t2, $t3, are_not_equal
    beq     $t2, $zero, are_equal
    addi    $t1, $t1, 1
    addi    $t0, $t0, 1
    j compare_str_loop